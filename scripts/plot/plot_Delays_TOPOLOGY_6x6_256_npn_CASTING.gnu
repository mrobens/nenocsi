#####################################################################################
#                                                                                    
# NENoCSi-Framework: Network traffic within NENoCSi generated by NEST
# Copyright (C) 2022-2023 Forschungszentrum Juelich GmbH, ZEA-2                      
# Author: Markus Robens <https://www.fz-juelich.de/profile/robens_m>                 
#                                                                                    
# This program is free software: you can redistribute it and/or modify               
# it under the terms of the GNU General Public License as published by               
# the Free Software Foundation, either version 3 of the License, or                  
# (at your option) any later version.                                                
#                                                                                    
# This program is distributed in the hope that it will be useful,                    
# but WITHOUT ANY WARRANTY; without even the implied warranty of                     
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the                      
# GNU General Public License for more details.                                       
#                                                                                    
# You should have received a copy of the GNU General Public License                  
# along with this program.  If not, see <https://www.gnu.org/licenses/>.             
#____________________________________________________________________________________
#                                                                                    
# This Gnuplot script creates a boxplot of all latencies observed in the network.    
# Due to the size of the input dataset, there is a large number of outliers that     
# can not be shown in a useful manner. Thus, the "nooutliers" option is passed to    
# the boxplot. However, the maximum value is indicated as well.                      
#                                                                                    
# The script needs to be called with                                                         
#  gnuplot -c <name of the script> <datadir> <topology> <casting> <val>              
# where <name of the script> is the file name of the script, <datadir> points        
# to the simulations data directory, <topology> is a string containing the           
# topology, <casting> is a string containing the abbreviation of the casting         
# type, and <val> is a numerical value indicating the acceleration factor (acc)      
#                                                                                    
#####################################################################################
INFILE = sprintf('%s/Delays/Delays_%s_6x6_256_npn_%s_acc_%03d.csv', ARG1, ARG2, ARG3, ARG4 + 0)
OUTFILE = sprintf('%s/Images/Delays_%s_6x6_256_npn_%s_acc_%03d.pdf', ARG1, ARG2, ARG3, ARG4 + 0)
set term pdfcairo font "Helvetica,12"
set output OUTFILE
stats INFILE nooutput
set style fill solid 0.5
set style boxplot nooutliers
set style boxplot medianlinewidth 2.0
set boxwidth 0.5
set border lw 2
set key L spacing 1.5
set xrange [0:2]
set yrange [0:1.1*STATS_max]
set xtics (sprintf('6x6 %s %s', ARG2, ARG3) 1) noenhanced
set ylabel 'Delays (cycles)'
set title sprintf("Acceleration factor %03d\nNo outliers shown", ARG4 + 0)
plot INFILE u (1.0):1 title 'NEST MC latency boxplot' w boxplot lw 2,\
'+' u (1.0):(STATS_mean) w p pt 12 lc 2 lw 2 ps 1.5 title 'NEST MC latency mean',\
'+' u (1.0):(STATS_max) w p pt 6 lc 2 lw 2 ps 1.5 title 'NEST MC latency max'